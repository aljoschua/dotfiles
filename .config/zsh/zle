#!/usr/bin/env zsh

function prepend-sudo {
    [[ -z $BUFFER ]] && BUFFER=$(fc -ln -1)

    [[ $BUFFER == sudo\ * ]] && return

    BUFFER="sudo $BUFFER"
    zle accept-line
}
zle -N prepend-sudo
bindkey '\es' prepend-sudo

function edit-command {
    zle -U "$BUFFER"
    [[ -z "$BUFFER" ]] && BUFFER=$(fc -ln -1)

    if zle expand-cmd-path; then
        BUFFER="${EDITOR:-vim} ${BUFFER%% *}"
        zle accept-line
    fi
}
zle -N edit-command
bindkey '\ee' edit-command

function splitting {
    echo $1
    echo $2
}

function launch-command {
    gnome-terminal -- $BUFFER
}
zle -N launch-command
bindkey '\et' launch-command

function foreground {
    zle -U "$BUFFER"
    BUFFER="fg"
    if [ "$NUMERIC" ]; then
        BUFFER+=" %$NUMERIC"
    fi
    zle accept-line
}
zle -N foreground
bindkey -M viins '^Z' foreground
bindkey -M vicmd '^Z' foreground
bindkey -M vicmd '^H' run-help
bindkey -M viins '^P' up-history
bindkey -M viins '^N' down-history
