prompt_segments2_help () {
    echo "Options:"
    echo "      nogit   Don't display git info in prompt"
}

prompt_segments2_setup () {
    prompt_segments2_opts=()
    if [ "$1" = nogit ]; then
        prompt_segments2_opts+=(nogit)
    fi
    PROMPT='%{%f%b%k%}$(prompt_segments2_make)'
    RPROMPT='%{%f%b%k%}$(prompt_segments2_make_right)'
    setopt TRANSIENTRPROMPT
    prompt_cleanup "setopt NOTRANSIENTRPROMPT"
    prompt_opts=(subst cr percent bang)
}

prompt_segments2_segment() {
  local seperator=' '
  if [ "$1" = -S ]; then
    seperator=''
    shift
  fi
  if [[ $(print -P "$3") = "$4" ]]; then
    return
  fi
  echo -n "%K{${1:-default}}%F{${2:-default}}%B$3%k%f%b$seperator"
}

prompt_segments2_git() {
  command git diff --quiet 2>/dev/null # Not very performant...
  case $? in
    0) color=green;;
    1) color=red;;
    128) ;; # In .git directory
    129) return;;
    *) echo "unknown git diff return code";;
  esac

  branch_name=$(command git rev-parse --abbrev-ref HEAD 2>/dev/null)
  case $branch_name in
    HEAD)
      commit_hash=$(command git rev-parse --short HEAD)
      prompt_segments2_segment '' $color d:$commit_hash
      ;;
    ^$)
      prompt_segments2_segment  '' $color i:0000000
      ;;
    *)
      prompt_segments2_segment  '' $color b:$branch_name
      ;;
  esac
}

prompt_segments2_jobs() {
  jobs -dl |
    sed -E '
  /^\[/ s/^\[([0-9]+)\] +[+-]? +([0-9]+) +(\w+) +(\(tty output\)|\(signal\))? +(.+)$/\1 \2 \3\n\5/;
      /^\(/ s/^\(pwd : (.*)\)$/\1/
    ' |
    while read jobno pid state; do
      read command
      read pwd
      case "$state" in
        suspended) pidcolor=red;;
        terminated) pidcolor=white;;
        running) pidcolor=green;;
        *) pidcolor=magenta;;
      esac
      prompt_segments2_segment -S '' $pidcolor $pid
      prompt_segments2_segment -S '' '' "|"
      prompt_segments2_segment -S '' '' "$pwd:" $(print -P %~:)
      prompt_segments2_segment '' '' "$command;"
    done
}

prompt_segments2_battery() {
  if (upower -d | grep -qe "state: \+discharging") 2>/dev/null; then
    upower -d | awk '/percentage/ { print $2 "%"; exit }'
  fi
}

## Main prompt
prompt_segments2_make() {
  local RETVAL=$?
  prompt_segments2_segment magenta white $RETVAL 0
  prompt_segments2_jobs
  prompt_segments2_segment blue white '%~' '~'
  prompt_segments2_segment '' '' '%(!,#,$)'
}

prompt_segments2_make_right() {
  prompt_segments2_segment cyan white '%n' aljoschua
  prompt_segments2_segment cyan white '@%m' @430-G5
  (( $prompt_segments2_opts[(Ie)nogit] )) || prompt_segments2_git
  prompt_segments2_segment cyan white $ZSH_VERSION
  prompt_segments2_segment magenta white $(prompt_segments2_battery)
}


prompt_segments2_setup "$@"
