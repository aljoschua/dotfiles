#!/usr/bin/env zsh

bindkey -v
function prepend-sudo {
    [[ -z $BUFFER ]] && BUFFER=$(fc -ln -1)

    [[ $BUFFER == sudo\ * ]] && return

    BUFFER="sudo $BUFFER"
    zle accept-line
}
zle -N prepend-sudo

function edit-command {
    zle push-input -w
    [[ -z "$BUFFER" ]] && BUFFER=$(fc -ln -1)

    if zle expand-cmd-path; then
        BUFFER="${EDITOR:-vim} ${BUFFER%% *}"
        zle accept-line
    fi
}
zle -N edit-command

function launch-command {
    BUFFER="gnome-terminal -- $BUFFER >/dev/null 2>&1"
    zle accept-line
}
zle -N launch-command

function foreground {
    zle push-input -w
    BUFFER="fg"
    if [ "$NUMERIC" ]; then
        BUFFER+=" %$NUMERIC"
    fi
    zle accept-line
}
zle -N foreground

bindkey -v '^U' prepend-sudo
bindkey -a '^U' prepend-sudo
bindkey -v '^E' edit-command
bindkey -s '^E' edit-command
bindkey -v '^T' launch-command
bindkey -a '^T' launch-command
bindkey -v '^Z' foreground
bindkey -a '^Z' foreground
bindkey -v '^O' insert-last-word
bindkey -a '^O' insert-last-word

bindkey -a '^H' run-help
bindkey -v '^H' run-help
bindkey -v '^P' up-history
bindkey -v '^N' down-history
